plugins {
    id "groovy"
    id "checkstyle"
    id "codenarc"
    id "idea"
    id "maven-publish"
    id "java-gradle-plugin"
    id "com.jfrog.bintray" version "1.8.1"
    id "com.github.hierynomus.license" version "0.14.0"
}

repositories {
    jcenter()
}

def compileAvroVersion = "1.8.2"

dependencies {
    compile localGroovy()
    compile "org.apache.avro:avro-compiler:${compileAvroVersion}"
    testCompile("org.spockframework:spock-core:1.2-groovy-2.4") {
        exclude module: "groovy-all"
    }
    testCompile gradleTestKit()
}

tasks.withType(AbstractCompile) {
    options.encoding = "UTF-8"
}
tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:all" << "-Xlint:-options" << "-Werror"
}
sourceCompatibility = 1.7

version = "0.15.2-SNAPSHOT"
group = "com.commercehub.gradle.plugin"

task sourcesJar(type: Jar, dependsOn: classes) {
    from sourceSets.main.allSource
    classifier "sources"
    extension "jar"
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc.destinationDir
    classifier "javadoc"
    extension "jar"
}

publishing {
    publications {
        mainMaven(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
        }
    }
}

bintray {
    // dryRun = true
    // publish = true
    user = project.hasProperty("bintrayUserName") ? bintrayUserName : null
    key = project.hasProperty("bintrayApiKey") ? bintrayApiKey : null
    publications = ["mainMaven", "avroBasePluginMarkerMaven", "avroPluginMarkerMaven"]
    pkg {
        repo = "main"
        name = project.name
        userOrg = "commercehub-oss"
        licenses = ["Apache-2.0"]
        desc = "A Gradle plugin to allow easily performing Java code generation for Apache Avro. It supports JSON schema declaration files, JSON protocol declaration files, and Avro IDL files."
        websiteUrl = "https://github.com/commercehub-oss/gradle-avro-plugin"
        issueTrackerUrl = 'https://github.com/commercehub-oss/gradle-avro-plugin/issues'
        vcsUrl = "https://github.com/commercehub-oss/gradle-avro-plugin"
        labels = ["serialization", "avro"]
        githubRepo = "commercehub-oss/gradle-avro-plugin"
        version {
            name = project.version
            vcsTag = project.version
        }
    }
}

bintrayUpload.dependsOn build, { generatePomFileForAvroBasePluginMarkerMavenPublication }, { generatePomFileForAvroPluginMarkerMavenPublication }

gradlePlugin {
    plugins {
        avro {
            id = "com.commercehub.gradle.plugin.avro"
            implementationClass = "com.commercehub.gradle.plugin.avro.AvroPlugin"
        }
        avroBase {
            id = "com.commercehub.gradle.plugin.avro-base"
            implementationClass = "com.commercehub.gradle.plugin.avro.AvroBasePlugin"
        }
    }
}

idea {
    project {
        vcs = "Git"
        ipr {
            withXml { provider ->
                def node = provider.asNode()
                node.append(new XmlParser().parseText("""
                <component name="ProjectCodeStyleSettingsManager">
                    <option name="PER_PROJECT_SETTINGS">
                        <value>
                            <option name="LINE_SEPARATOR" value="&#10;"/>
                            <option name="RIGHT_MARGIN" value="140"/>
                        </value>
                    </option>
                    <option name="USE_PER_PROJECT_SETTINGS" value="true"/>
                </component>
                """.stripIndent()))
            }
        }
    }
}

// Write the plugin's classpath to a file to share with the tests
task createClasspathManifest {
    def outputDir = file("$buildDir/$name")

    inputs.files sourceSets.main.runtimeClasspath
    outputs.dir outputDir

    doLast {
        outputDir.mkdirs()
        file("$outputDir/plugin-classpath.txt").text = sourceSets.main.runtimeClasspath.join("\n")
    }
}

// Add the classpath file to the test runtime classpath
dependencies {
    testRuntime files(createClasspathManifest)
}

checkstyle {
    toolVersion = "6.1.1" // Last version of checkstyle to support Java 6
}
// Nasty workaround for https://issues.gradle.org/browse/GRADLE-2888
def checkstyleWarningsFile = "build/reports/checkstyle/main.xml"
checkstyleMain.doLast {
    File warningsFile = file(checkstyleWarningsFile)
    if (warningsFile.exists() && warningsFile.text.contains("<error ")) {
        throw new GradleException("There were checkstyle warnings! For more info check ${warningsFile}")
    }
}
// In Gradle 4.8 the checkstyle basedir changed to no longer be the project root by default; thus we need to specify
checkstyleMain {
    configProperties = ['basedir': "$rootDir/config/checkstyle"]
}

codenarc {
    toolVersion = "1.0"
    config = project.resources.text.fromFile("config/codenarc/codenarc.groovy")
}

tasks.create("testVersionCompatibility") {
    description = "Tests cross-compatibility of the plugin with different versions of Avro and Gradle."
    group = "Verification"
}

tasks.create("testRecentVersionCompatibility") {
    description = "Tests cross-compatibility of the plugin with recent versions of Avro and Gradle."
    group = "Verification"
}

test {
    systemProperties = [
        avroVersion: compileAvroVersion,
        gradleVersion: gradle.gradleVersion,
    ]
}

def avroVersions = ["1.8.2"]
def gradleVersions = []
if (!org.gradle.api.JavaVersion.current().isJava9Compatible()) { // Gradle 4.2.1 is the first version that supports modern Java 9, as per https://discuss.gradle.org/t/could-not-determine-java-version-from-9-0-1/24457
    gradleVersions.addAll("3.0", "3.1", "3.2", "3.2.1", "3.3", "3.4", "3.4.1", "3.5", "3.5.1", "4.0", "4.0.1", "4.0.2", "4.1", "4.2")
}
if (!org.gradle.api.JavaVersion.current().isJava11Compatible()) { // Gradle 4.8 appears to be the first version that supports Java 11, as per https://github.com/gradle/gradle/pull/4759 and testing
    gradleVersions.addAll("4.2.1", "4.3", "4.3.1", "4.4", "4.4.1", "4.5", "4.5.1", "4.6", "4.7")
}
gradleVersions.addAll("4.8", "4.8.1", "4.9", "4.10", "4.10.1", "4.10.2")
// Support for running Gradle on Java 7 is deprecated and will be removed in Gradle 5, as per https://docs.gradle.org/4.2.1/release-notes.html

avroVersions.each { def avroVersion ->
    gradleVersions.each { def gradleVersion ->
        def newTask = tasks.create(name: "testAvro${avroVersion}Gradle${gradleVersion}", type: Test) {
            description = "Test cross-compatibility of the plugin with Avro ${avroVersion} and Gradle ${gradleVersion}"
            systemProperties = [
                avroVersion: avroVersion,
                gradleVersion: gradleVersion,
            ]
            reports {
                html.destination = file("$buildDir/reports/tests-${avroVersion}-${gradleVersion}")
                junitXml.destination = file("$buildDir/reports/tests-${avroVersion}-${gradleVersion}")
            }
        }
        testVersionCompatibility.dependsOn newTask
        if (gradleVersions.indexOf(gradleVersion) >= gradleVersions.size() - 5) {
            testRecentVersionCompatibility.dependsOn newTask
        }
    }
}

tasks.withType(Test) {
    jvmArgs "-Xss320k"
    minHeapSize "120m"
    maxHeapSize "280m"
}

license {
    header = file("gradle/DEFAULT_LICENSE_NOTICE")
    skipExistingHeaders = true
    mapping("avdl", "JAVADOC_STYLE")
    excludes(["**/*.avsc", "**/*.avpr"]) // JSON doesn't allow comments
    exclude("**/record.vm") // Existing header in different style
    // dryRun = true
}
